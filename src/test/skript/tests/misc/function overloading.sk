local function overloaded(x: int) :: int:
	return 1

local function overloaded(x: text) :: int:
	return 2

test "function overloading with 1 parameter":
	set {_x} to true

	parse:
		overloaded({_x})

	assert last parse logs contains "Skript cannot determine which function named 'overloaded' to call. Try clarifying the type of the arguments using the 'value within' expression."

	set {_x} to 1

	assert overloaded(number within {_x}) = 1

function overloaded(x: int, y: int) :: int:
	return 1

function overloaded(x: int, y: int, z: int) :: int:
	return 2

function overloaded(x: text, y: text) :: int:
	return 3

function overloaded(x: text, y: int, z: int) :: int:
	return 4

test "function overloading with 2 parameters":

	# literal overloading
	assert overloaded(1, 2) = 1
	assert overloaded(1, 2, 3) = 2
	assert overloaded("a", "b") = 3
	assert overloaded("a", 2, 3) = 4

	# overloading with unknown type
	assert overloaded(1, {_x}) = 1
	assert overloaded({_x}, 1) = 1

	# can't determine the type, so error
	parse:
		overloaded({_y}, {_x})

	assert last parse logs contains "Skript cannot determine which function named 'overloaded' to call. Try clarifying the type of the arguments using the 'value within' expression."
